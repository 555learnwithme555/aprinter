include "http_server.ncdi"
include "job_queue.ncdi"
include "run_process_output.ncdi"
include "temp_dir.ncdi"

process main {
    log_fr(@notice, {"Init"}, {"Exit"});
    
    # Read the configuration.
    getargs() args;
    value(args) args;
    args->get("0") config_path;
    file_read(config_path) config_contents;
    parse_value(config_contents) config;
    value(config) config;
    
    # Fetch stuff out of the configuration.
    config->get("http_server") http_server_config;
    config->get("max_concurrent_compiles") max_concurrent_compiles;
    config->get("temp_dir") temp_dir;
    config->get("mktemp") mktemp;
    config->get("rm") rm;
    
    # Init the queue.
    call(@job_queue, {max_concurrent_compiles}) queue;
    
    # Define the HTTP request handler.
    Block {
        alias(@_arg0) method;
        alias(@_arg1) path;
        alias(@_arg2) headers;
        alias(@_arg3) request_payload;
        objref_arg(_arg4) callbacks;
        objref_arg(_arg5) responses;
        
        # A little bit of hacking to handle errors without nesting.
        var(@true) cont;
        backtrack_point() abort;
        If (cont) {
            cont->set(@false);
            
            # Provide some warm and fuzzy text at /.
            If (@val_equal(path, "/")) {
                If (@not(@val_equal(method, "GET"))) {
                    responses.method_not_allowed->call();
                    abort->go();
                };
                
                value("") text;
                text->append("Hello curious user! You have reached the APrinter Compilation Service Backend.\n\n");
                text->append("Unfortunately there is nothing here to see.\n\n");
                text->append("Unless you are interested in hot new programming languages.\n");
                text->append("In that case you really should take a look at the source code of this software, ");
                text->append("which is written in the NCD Programming Language :)");
                
                callbacks.add_payload->call(text);
                abort->go();
            };
            
            # Unsupported request path?
            If (@not(@val_equal(path, "/compile"))) {
                responses.not_found->call();
                abort->go();
            };
            
            # Here we only support POST.
            If (@val_different(method, "POST")) {
                responses.method_not_allowed->call();
                abort->go();
            };
            
            # Create a temporary directory.
            call(@temp_dir, {^callbacks.log, temp_dir, mktemp, rm}) temp_dir;
            
            # Creation failed?
            If (@not(temp_dir.succeeded)) {
                responses.internal_error->call();
                abort->go();
            };
            
            var(@true) build_do;
            backtrack_point() build_done;
            If (build_do) {
                build_do->set(@false);
                
                # Wait in the queue until we're allowed to do the Compilation.
                call(@job_queue_request, {^queue});
                
                callbacks.add_payload->call("Starting build\n");
                
                sleep("2000");
                
                build_done->go();
            };
            
            callbacks.add_payload->call("Build completed\n");
        };
    } request_handler;
    
    # Start the HTTP server.
    call(@http_server, {http_server_config, ^request_handler});
}
